::TokenListBuilder
  @parsed\%.TokenList|A|O|s\$
  @xformer&\LineTransformer\?
  @tokens\%.TokenList\

:build\%.TokenList\
  if (!@tokens)
    @tokens = new %.TokenList()
    @build_rec(@parsed)
  => @tokens

:result\%.Result\
  input\!A.<InputLine>\$
  @.build()
  => new %.Result(@tokens, input)

:@build_rec
  data\%.TokenList|A|O|s\$
  if (data instanceof %.TokenList)
    @add_tokens(data)
    =>

  if (data instanceof Array)
    @add_array(data)
    =>

  if (data instanceof Object)
    @add_object(data)
    =>

  // Must be a string.
  if (data)
    @tokens.add(data)

:@add_tokens
  data\%.TokenList\$
  @tokens.add(data)

:@add_array
  data\A\$
  data.forEach(##)
    elem\%.TokenList|A|O|s\$
    @build_rec(elem)

:@add_object
  data\O\$
  if (data.g)
    p := data.params
    switch(data.g)
      case 'c': // Current package ref.
      str := p['percents'] + '.' + p.name
      @tokens.add(@.xformer ? @.xformer.pkg_ref(str) : str)
      break

      case 'e': // Parent call.
      @tokens.add(@.xformer ? @.xformer.parent_call(#) : ['%(', p.args, ')'])
        new %.TokenListBuilder(p.args, @.xformer).build().toString()
      break

      case 'm': // Marker.
      @tokens.add(new %.BlockMarker(p.type))
      break

      case 'p': // Param line.
      t := @tokens
      @tokens = new %.ParamLine(#)
        p.name
        p.member
        p.access
        new %.ParamLineBuilder(p.type, @.xformer).build().toString()
        p.marker
        new %.TokenListBuilder(p.init, @.xformer).build()
      @tokens.add(t)
      break

      case 's': // Separator line.
      @tokens.grammar = 's'
      break

      case 't': // Type literal.
      @.add_type_object(p)
      break

  if (data.t)
    @tokens.add(new %.TokenListBuilder(data.t, @.xformer).build())
  if (data.p)
    @tokens.prepend(new %.TokenListBuilder(data.p, @.xformer).build())
  if (data.a)
    @tokens.append(new %.TokenListBuilder(data.a, @.xformer).build())

:add_type_object
  params\O\$
  @tokens.add(@.xformer ? @.xformer.cast(params.type) : params.tokens)


::ParamLineBuilder <%.TokenListBuilder
  parsed\%.TokenList|A|O|s\$
  xformer&\LineTransformer\?
  %(parsed, xformer)

:add_type_object
  params\O\$
  @tokens.add(@.xformer ? @.xformer.type(params.type) : params.tokens)
