::
  @parsed\%.TokenList|A|O|s\$
  @xformer&\LineTransformer\?
  @tokens\%.TokenList\

:build\%.TokenList\
  if !@tokens
    @tokens = &%.TokenList()
    @buildRec(@parsed)
  => @tokens

:result\%.Result\
  line\!A.<input.Line>\$
  @.build()
  => &%.Result(@tokens)

:@buildRec
  data\%.TokenList|A|O|s\$
  if data instanceof %.TokenList
    @addTokens(data)
    =>

  if data instanceof Array
    @addArray(data)
    =>

  if data instanceof %.BlockMarker
    @tokens.add(data)
    =>

  if data instanceof Object
    @addObject(data)
    =>

  // Must be a string.
  if data
    @tokens.add(data)

:@addTokens
  data\%.TokenList\$
  @tokens.add(data)

:@addArray
  data\A\$
  data.forEach(##)
    elem\%.TokenList|A|O|s\$
    @buildRec(elem)

:@addObject
  data\O\$
  if data.g
    p := data.params
    switch data.g
      case 'p' // Param line.
      t := @tokens
      @tokens = &%.ParamLine(#)
        p.name
        p.member
        p.access
        &%.TokenListBuilder(p.type, @.xformer).build().toString()
        p.marker
        &%.TokenListBuilder(p.init, @.xformer).build()
      @tokens.add(t)
      break

      case 's' // Separator line.
      @tokens.grammar = 's'
      break

  if data.t
    @tokens.add(&%.TokenListBuilder(data.t, @.xformer).build())
  if data.p
    @tokens.prepend(&%.TokenListBuilder(data.p, @.xformer).build())
  if data.a
    @tokens.append(&%.TokenListBuilder(data.a, @.xformer).build())
