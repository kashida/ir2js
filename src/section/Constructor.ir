::^%.Callable
  context\context.Context\$
  @parent\s|-\?
  context.isCtor = true
  ^(context, '')
  @parent = @parent ? @.context.pkg.replace(@parent) : ''

:create = ##
  \%.Constructor|-\
  scope\FileScope\$
  line\s\$
  re := /^\:\s*(\w*)\s*(\^\s*(.*\S))?$/.exec(line)
  if !re
    => null

  // need to keep this in a member var too.
  scope.context.cls = &context.Class()
  ctor := &%.Constructor(scope.copyContextWithName(re[1]), re[3])
  scope.context.cls.ctor = ctor
  scope.types.addCtor(ctor.name())
  if re[3]
    scope.types.setParent(ctor.parentName())
  => ctor

:parentName\s\
  => \s\(@parent)

:transform^
  assert(@.numBlocks() == 1, @.lines[0])
  @.params = &ParamSet(@.context, @.block(0), true)
  @.params.transform()
  @.block(0).transform()

:output\A\
  decl := @.params.outputDecls()
  decl.push('@constructor')
  inherit := []
  if @parent
    decl.push('@extends {' + @parent + '}')
    inherit.push([#].join(''))
      'goog.inherits('
      @.context.name.ref
      ', '
      @parent
      ');'
  decl.push('@struct')
  decl.push('@suppress {checkStructDictInheritance}')
  => [#]
    docLines(decl)
    @.outputFunc()
    whitespaces(@.block(0).indent) + 'var self = this;'
    @.outputBody('};')
    inherit
    [#].join('')
      @.context.name.property('_classname').decl
      " = '"
      @.context.name.ref
      "';"
    @.context.cls.outputAccessors()

:setType^
  types\type.Set\$
  @.params.setArgTypes(types.getCurrentCtor())
