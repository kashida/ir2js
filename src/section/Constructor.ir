::^%.Callable
  context\context.Context\$
  tmpl_vars\A<s>\$
  @parent\s|-\?
  @parentTmplVars\s|-\?
  context.isCtor = true
  ^(context, '', tmpl_vars)
  @parent = @parent ? @.context.pkg.replace(@parent) : ''

:create = ##
  \%.Constructor|-\
  scope\FileScope\$
  line\s\$
  re := /^\:\s*(\w*)\s*(\<\s*([\w\,]+)\s*\>\s*)?(\^\s*(.*\w)\s*(\<([\w\s,]*)\>)?)?\s*$/.exec(line)
  if !re
    => null

  scope.context.cls = &context.Class()
  ctor := &%.Constructor(scope.copyContextWithName(re[1]), #, re[5], #)
    re[2] ? re[3].split(',').map(##) : []
      name\s\$
      => name.trim()
    re[7] && re[7].replace(/\s/g, '') || undefined
  scope.context.cls.ctor = ctor
  scope.types.addCtor(ctor.name())
  if re[5]
    scope.types.setParent(ctor.parentName())
  => ctor

:parentName\s\
  => \s\(@parent)

:transform^
  assert(@.numBlocks() == 1, @.lines[0])
  @.params = &ParamSet(@.context, @.block(0), true)
  @.params.templateParams = @.tmplVars
  @.params.transform()
  @.block(0).transform()

:output\A\
  decl := @.params.outputDecls()
  decl.push('@constructor')
  inherit := []
  if @parent
    decl.push('@extends {' + @parent + # + '}')
      @parentTmplVars ? '.<' + @parentTmplVars + '>' : ''
    inherit.push([#].join(''))
      'goog.inherits('
      @.context.name.ref
      ', '
      @parent
      ');'
  decl.push('@struct')
  decl.push('@suppress {checkStructDictInheritance}')
  => [#]
    docLines(decl)
    @.outputFunc()
    whitespaces(@.block(0).indent) + 'var self = this;'
    @.outputBody('};')
    inherit
    [#].join('')
      @.context.name.property('_classname').decl
      " = '"
      @.context.name.ref
      "';"
    @.context.cls.outputAccessors()

:setType^
  types\type.Set\$
  @.params.setArgTypes(types.getCurrentCtor())
