::^%.Callable
  context\context.Context\$
  tmpl_vars\A<s>\$
  @parent\s|-\$
  @parentTmplVars\A<s>\$
  context.isCtor = true
  ^(context, '', tmpl_vars)
  @parent = @parent ? @.context.pkg.replace(@parent) : ''

:re = [#]
  /\:/
  {name: /\w+/, _opt: true}
  &re.TmplVarList('tmplVars')
  {parent: [#], _opt: true}
    /\^/
    {parentName: /[\w\.\%]+/}
    &re.TmplVarList('pTmplVars')

:create = ##
  \%.Constructor|-\
  scope\FileScope\$
  line\s\$
  m := &re.Compiler(%.Constructor.re).eval(line)
  if !m
    => null

  scope.context.cls = &context.Class()
  ctor := &%.Constructor(#)
    scope.copyContextWithName(m.name)
    m.tmplVars
    m.parentName
    m['pTmplVars']
  scope.context.cls.ctor = ctor
  scope.types.addCtor(ctor.name())
  if m.parentName
    scope.types.setParent(ctor.parentName())
  => ctor

:parentName\s\
  => \s\(@parent)

:transform^
  assert(@.numBlocks() == 1, @.lines[0])
  @.params = &ParamSet(@.context, @.block(0), true)
  @.params.templateParams = @.tmplVars
  @.params.transform()
  @.block(0).transform()

:output\A\
  decl := @.params.outputDecls()
  decl.push('@constructor')
  inherit := []
  if @parent
    decl.push('@extends {' + @parent + # + '}')
      @parentTmplVars.length ? '.<' + @parentTmplVars.join(',') + '>' : ''
    inherit.push([#].join(''))
      'goog.inherits('
      @.context.name.ref
      ', '
      @parent
      ');'
  decl.push('@struct')
  decl.push('@suppress {checkStructDictInheritance}')
  => [#]
    docLines(decl)
    @.outputFunc()
    whitespaces(@.block(0).indent) + 'var self = this;'
    @.outputBody('};')
    inherit
    [#].join('')
      @.context.name.property('_classname').decl
      " = '"
      @.context.name.ref
      "';"
    @.context.cls.outputAccessors()

:setType^
  types\type.Set\$
  @.params.setArgTypes(types.getCurrentCtor())
