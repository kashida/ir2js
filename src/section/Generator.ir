::Generator
  @scope\FileScope\$

:generate\%.Code\
  header\input.Line\$
  lines\A.<input.Line>\$
  section := null
  header_line := header.line.substr(1)
  if ![#].some(##)
    '_createCtor'
    '_createMethod'
    '_createAccessor'
    '_createGlobalFunction'
    '_createMultiLineStr'
    '_createGlobalCode'
    '_createNativeCode'
    '_createAnonymousScope'
    //'_createInterface'
    '_createTypedef'
    //'_createVar' -- any type including array, map, number, etc.
    //'_createClassContext' -- for adding methods to e.g. Object.
    --
    method\s\$
    section = @[method].call(@, header_line, header)
    if section
      section.lines = lines
      section.close(@scope.context.fileName, @scope.context.pkg)
      section.setType(@scope.types)
    => !!section
    --
    error(header, 'line starts with colon and not a code section marker')
  => section

:@createCtor\%.Constructor\
  line\s\$
  re := /^\:\s*(\w+)\s*(\<\s*(.*\S))?$/.exec(line)
  if !re
    => null

  // need to keep this in a member var too.
  @scope.context.cls = &context.Class()
  ctor := &%.Constructor(@scope.copyContextWithName(re[1]), re[3])
  @scope.context.cls.ctor = ctor
  @scope.types.addCtor(ctor.name())
  if re[3]
    @scope.types.setParent(ctor.parentName())
  => ctor

:@createMethod\%.Method\
  line\s\$
  header\input.Line\$
  re := /^(\<?)(\@?)\s*([a-zA-Z]\w*)\s*(\\(.*)\\)?$/.exec(line)
  if !re
    => null

  // we should have seen a ctor.
  if !@scope.context.cls
    error(header, 'method marker w/o class')
    => null
  => &%.Method(#)
      @scope.copyContext(@scope.context.cls.methodName((re[2] ? '_' : '') + re[3]))
      re[5]
      !!re[1]

:@createAccessor\%.Accessor\
  line\s\$
  header\input.Line\$
  re := /^([+*])\s*([a-zA-Z]\w*)\s*(\\(.*)\\)?$/.exec(line)
  if !re
    => null

  // we should have seen a ctor.
  if !@scope.context.cls
    error(header, 'accessor marker w/o class')
    => null
  type := re[1]
  name := re[2]
  ret_type := re[4]
  ctx := @scope.copyContext(@scope.context.cls.methodName(name))
  => &%.Accessor(ctx, name, ret_type, type == '+')

:@createGlobalFunction\%.Function\
  line\s\$
  re := /^=\s*(\w+)\s*##(\\(.*)\\)?$/.exec(line)
  if !re
    => null
  => &%.Function(@scope.copyContextWithName(re[1]), re[3])

:@createMultiLineStr\%.Str\
  line\s\$
  re := /^'\s*(\w+)$/.exec(line)
  if !re
    => null
  => &%.Str(@scope.copyContextWithName(re[1]))

:@createGlobalCode\%.Global\
  line\s\$
  => line == '' ? &%.Global() : null

:@createNativeCode\%.Native\
  line\s\$
  => line == '~' ? &%.Native() : null

:@createAnonymousScope\%.Scope\
  line\s\$
  => line == '{' ? &%.Scope() : null

:@createTypedef\%.Typedef\
  line\s\$
  re := /^\!\s*(\w+)$/.exec(line)
  if !re
    => null
  => &%.Typedef(@scope.copyContextWithName(re[1]))
