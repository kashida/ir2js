::TestCase
  @name\s\$
  @packageName\s\$
  @input\A.<s>\$
  @output\A.<s>\$
  @isGlobal\b\$
  @failed+\b\ false

:run
  c := @isGlobal ? (#) : (#)
    &FileScope(@name, @packageName)
    --
    &CodeScope(&context.Context(@name, &context.Package('')))

  actual_output := ''
  try
    c.processLines(@input)
    actual_output = c.output()
    if @isGlobal
      type_str := objStringify(c.types.extract(), true)
      if type_str
        actual_output = actual_output.concat(type_str.split('\n'))
  catch e
    console.log('EXCEPTION: ' + @name)
    @warnWithIndent('input', @input)
    @warnWithIndent('expected', @output)
    throw e

  if actual_output.join('\n') == @output.join('\n')
    console.log('PASS: ' + @name)
  else
    console.log('FAIL: ' + @name)
    @warnWithIndent('input', @input)
    @warnWithIndent('expected', @output)
    @warnWithIndent('actual', actual_output)
    @warnWithIndent('diff', @makeDiff(@output, actual_output))
    @failed = true

:@warnWithIndent
  title\s\$
  content\A.<s>\$
  console.log('  ' + title + ':')
  warn_rec := ##
    lines\s|A\$
    level\n\$
    if typeof(lines) == 'string'
      console.log(whitespaces(4 + level * 2) + lines)
    else
      if lines instanceof output.Line
        lines = lines.output
      lines.forEach(##)
        line\s|A\$
        warn_rec(line, level + 1)
  warn_rec(content, 0)

:@makeDiff
  lines0\A.<s>\$
  lines1\A.<s>\$
  result := []
  lines0.forEach(##)
    line0\s\$
    i\n\$
    if lines1.length <= i
      result.push('- ' + line0)
      =>
    line1 := lines1[i]
    if line0 != line1
      result.push('< ' + line0)
      result.push(' >' + line1)
    else
      result.push('= ' + line0)
  lines1.slice(lines0.length).forEach(##)
    line\s\$
    result.push('+ ' + line)
  => result

:
  exports.TestCase = TestCase
