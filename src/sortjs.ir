::StringSet
  @list\A.<s>\ []
  @map\O.<s, b>\ {}

:toString\s\
  => @list.join('|')

:list\A.<s>\
  => @list

:size\n\
  => @list.length

:has\b\
  str\s\$
  => @map[str]

:addAll
  strs\A.<s>\$
  strs.forEach(##)
    str\s\$
    @.add(str)

:add
  str\s\$
  @list.push(str)
  @map[str] = true

:filterOut\A.<s>\
  strs\A.<s>\$
  // remove the strings that are in this set.
  => strs.filter(##)
    f\s\$
    => !@map[f]


::ClassDeps
  // Maps class name to file name where its defined.
  @where\O.<s, s>\ {}
  // Maps file name to array of required class names.
  @depends\O.<s, A.<s>>\ {}

:toString\s\
  => Object.keys(\!Object\(@depends)).map(##).join('')
    k\s\$
    list := @depends[k]
    => list.length ? '[' + k + ':' + list.join('|') + ']' : ''

:load
  files\A.<s>\$
  files.forEach(##)
    file\s\$
    @depends[file] = []
    tk := JSON.parse(_fs.readFileSync(file.replace(/\.js/, '.tk'), 'utf-8'))
    tk['cls'].forEach(##)
      cls\*\$
      @where[cls.name] = file
      if cls['parent']
        @depends[file].push(cls['parent'])
    // remove self dependencies.
    @depends[file] = @depends[file].filter(##)
      dep\s\$
      => @where[dep] != file

:hasDeps\b\
  file\s\$
  dep := @depends[file]
  => !!dep && !!dep.length

:removeDeps
  file\s\$
  provided_files\StringSet\$
  @depends[file] = @depends[file].filter(##)
    dep\s\$
    i\n\$
    => !provided_files.has(@where[dep])


:
  exports.createSortedList = ##
    \A.<s>\
    files\A.<s>\$
    deps := &ClassDeps()
    deps.load(files)

    // sort the files in inheritance order.
    all := files.concat()
    sorted := &StringSet()
    while all.length
      found := &StringSet()
      all.forEach(##)
        f\s\$
        // remove the dependencies already satisfied.
        deps.removeDeps(f, sorted)

        if !deps.hasDeps(f)
          found.add(f)

      if !found.size()
        // no progress. something's wrong.
        console.log('remaining deps: ' + deps)
        throw 'circular inheritance dependencies'

      sorted.addAll(found.list())

      // remove all the found files.
      all = found.filterOut(all)
    => sorted.list()
