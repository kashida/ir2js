output lines corresponds to one input line.
::LineOutput
  @input\InputLine\$
  @indent&\n\ input.indent
  @prefix_lines\A.<s>\ []
  @lines\A.<BlockOutput|s>\ []
  @line_prefix&\s\ ''
  @line_suffix&\s\ ''
  @tail_comment&\A.<s>\ []

:append_prefix_line
  line\s\$
  @prefix_lines.push(line)

:append_line
  line\s\$
  @lines.push(line)

:append_block
  block\BlockOutput\$
  @lines.push(block)

:append_lines
  lines\A.<s>\$
  lines.forEach(##)
    line\s\$
    @lines.push(line)

:remove_empty_lines
  @lines = @lines.filter(##)
    line\BlockOutput|s\$
    => line

:+output\A.<s>\
  result := []
  indent := whitespaces(@indent)
  @prefix_lines.forEach(##)
    line\s\$
    result.push(line ? indent + line : '')
  @lines.forEach(##)
    line\s|BlockOutput\$
    i\n\$
    if line instanceof BlockOutput
      result = result.concat(line.output)
    else
      to_add := line
      if i == 0 && @line_prefix
        to_add = @line_prefix + to_add
      if i == @lines.length - 1 && @line_suffix
        to_add += @line_suffix
      if to_add
        to_add = indent + to_add
      result.push(to_add)
  @tail_comment.forEach(##)
    c\s\$
    result.push(indent + c)
  => result
