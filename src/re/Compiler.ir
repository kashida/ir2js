The pattern is expressed as a tree of RegExp, Arrays, and Objects.
RegExp should not include parens. Arrays are sequence of patterns.
Each member of Objects are converted to alternative pattens. The result Objects
will have field names mathcing these object field names.
There are two meta data in the Object.
_opt (boolean, default false): makes the pattern optional (by appending '?').
_spc (boolean, default true): add any number of spaces '\s*' before and after.

The result of eval is either null (when the match afils), or an Object.
Object has resulting partial matches.

The regexp is always bound to both begenning and end (^ and $ added
automatically).

::
  @pattern\O\$
  @extractors\A<%.Extractor|->\ []

:eval\O|-\
  str\s\$
  regex := @build()
  match := regex.exec(str)
  if !match
    => null

  result := {}
  @extractors.forEach(##)
    extractor\%.Extractor|-\$
    i\n\$
    extractor && extractor.set(\A<s>\(match), i, result)
  => result

:@build\RegExp\
  @extractors.push(null)
  => &RegExp('^' + @buildReStr(@pattern) + '$')

:@buildReStr\s\
  pattern\O\$
  if pattern instanceof RegExp
    @extractors.push(null)
    pstr := pattern.toString()
    => '(' + pstr.substring(1, pstr.length - 1) + ')'

  if pattern instanceof Array
    @extractors.push(null)
    => '(' + pattern.map(##).join('') + ')'
      item\O\$
      => @buildReStr(item)

  => @buildReStrWithMap(pattern)

:@buildReStrWithMap
  pattern\O\$
  @extractors.push(null)
  alts := []
  each name in pattern
    if name == '_opt' || name == '_spc'
      continue

    if name != '_'
      @setCurrent(name)
    alts.push(@buildReStr(pattern[name]))

  opt := !!pattern._opt ? '?' : ''
  spc := pattern['_spc'] == false ? '' : '\\s*'
  => spc + '(' + alts.join('|') + ')' + opt + spc

:@setCurrent
  name\s\$
  @extractors[@extractors.length - 1] = &%.Extractor(name)
