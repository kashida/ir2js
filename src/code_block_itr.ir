::CodeBlockItr
  @input\input.Line\$
  @code\A.<parser.BlockMarker|s>\$
  @blocks\A.<IndentBlock>\$
  @cb&\?f(s, b)\
  @extra_cb&\?f(s, b)\
  @cidx+\n\ 0
  @bidx+\n\ 0
  @lidx+\n\ 0
  @pidx+\n\ 0
  @extra_block+\b\ false

Valid only after run call.
:+ends_with_code\b\
  => #&&
    @code.length
    !(@code[@code.length - 1] instanceof parser.BlockMarker)
    !@extra_block

:run
  @code.forEach(##)
    frg\parser.BlockMarker|s\$
    i\n\$
    @cidx = i
    if frg instanceof parser.BlockMarker
      @handle_marker(frg)

  if @lidx > 0
    if @cb
      @cb('l', false)
    @lidx = 0
    @bidx++

  // There may be one extra block.
  if @bidx > @blocks.length || @bidx + 1 < @blocks.length
    warn(@input, '# blocks does not match #markers.')
    => false

  if @bidx < @blocks.length
    @extra_block = true
    @cidx++
    if @cb
      @cb('f', false)
    @bidx++
  => true

:@handle_marker
  marker\parser.BlockMarker\$
  switch marker.type
    case 'f'
    if @lidx > 0
      if @cb
        @cb('l', false)
      @lidx = 0
      @bidx++
    if @cb
      @cb('f', true)
    @bidx++
    @pidx++
    break

    case 'l'
    @lidx++
    break

    default
    if @lidx > 0
      if @cb
        @cb('l', false)
      @lidx = 0
      @bidx++
    if @cb
      @cb(marker.type, false)
    @bidx++
    @lidx = 0
    break
